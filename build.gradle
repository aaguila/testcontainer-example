plugins {
    id 'java'
}

group 'com.qajungle'
version '1.0-SNAPSHOT'

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "io.vertx:vertx-core:3.6.2"
    compile 'io.vertx:vertx-web:3.6.2'
    compile 'io.vertx:vertx-redis-client:3.6.2'

    compileOnly 'io.vertx:vertx-codegen:3.6.2'

    testCompile 'io.vertx:vertx-unit:3.4.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task annotationProcessing(type: JavaCompile, group: 'build') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-AoutputDirectory=${destinationDir.absolutePath}"
    ]
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    dependsOn annotationProcessing
}

jar {
    // by default fat jar
    archiveName = 'vertx-testing-example-fat.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'com.qajungle.todolist.verticles.SingleApplicationVerticle'
    }
}